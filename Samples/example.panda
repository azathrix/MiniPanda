// MiniPanda 示例脚本
// 这是一个展示 MiniPanda 语法的示例文件

// ========== 变量 ==========
var name = "MiniPanda"
var version = 1.0
var isReady = true

print("欢迎使用 " + name + " v" + version)

// ========== 数组 ==========
var fruits = ["苹果", "香蕉", "橙子"]
fruits[1] = "葡萄"

print("水果列表:")
for fruit in fruits {
    print("  - " + fruit)
}

global var test = 1



// ========== 对象 ==========
var config = {
    debug: true,
    maxFps: 60,
    volume: 0.8
}



print("配置: debug=" + config.debug + ", maxFps=" + config.maxFps)

// ========== 函数 ==========
global func greet(name) {
    return "你好, " + name + "!"
}

func fibonacci(n) {
    if n <= 1 return n
    return fibonacci(n - 1) + fibonacci(n - 2)
}

print(greet("世界"))
print("斐波那契(10) = " + fibonacci(10))

// ========== Lambda ==========
var square = (x) => x * x
var cube = (x) => x * x * x


print("5 的平方 = " + square(5))
print("3 的立方 = " + cube(3))

// ========== 类 ==========
global class Entity {
    Entity(name) {
        this.name = name
        this.hp = 100
    }

    func takeDamage(amount) {
        this.hp = this.hp - amount
        if this.hp < 0 {
            this.hp = 0
        }
        print(this.name + " 受到 " + amount + " 点伤害, 剩余 HP: " + this.hp)
    }

    func isAlive() {
        return this.hp > 0
    }
}


class Player : Entity {

    Player(name, level) {
        super.Entity(name)
        this.level = level
        this.hp = 100 + level * 10
    }

    //升级
    func levelUp() {
        this.level = this.level + 1
        this.hp = this.hp + 10
        print(this.name + " 升级到 " + this.level + " 级!")
    }
}

var hero = Player("勇者", 1)


print("创建玩家: " + hero.name + ", 等级: " + hero.level + ", HP: " + hero.hp)

hero.takeDamage(30)
hero.levelUp()
hero.takeDamage(50)



if hero.isAlive() {
    print(hero.name + " 还活着!")
} else {
    print(hero.name + " 已阵亡...")
}

// ========== 控制流 ==========
var sum = 0
for i in range(10) {
    if i % 2 == 0 continue
    sum = sum + i
}
print("1-9 奇数和 = " + sum)

var count = 0
while count < 5 {
    print("计数: " + count)
    count = count + 1
}

print("示例运行完成!")

// ========== 模块导入 (Import) ==========
// 注意: 需要先用 LoadModule 预加载模块文件

// 基本导入 - 文件作用域
import "utils" as u



print("Utils VERSION: " + u.VERSION)
u.helper()
print("clamp(15, 0, 10) = " + u.clamp(15, 0, 10))
print("lerp(0, 100, 0.5) = " + u.lerp(0, 100, 0.5))

// 路径转换 - 点号转目录
import "math.vector" as vec


var v1 = vec.create(1, 2, 3)
var v2 = vec.create(4, 5, 6)
print("v1 = (" + v1.x + ", " + v1.y + ", " + v1.z + ")")

var v3 = vec.add(v1, v2)
print("v1 + v2 = (" + v3.x + ", " + v3.y + ", " + v3.z + ")")

print("dot(v1, v2) = " + vec.dot(v1, v2))
print("length(v1) = " + vec.length(v1))

